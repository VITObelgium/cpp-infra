get_filename_component(Infra_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH ${Infra_CMAKE_DIR})

find_dependency(fmt CONFIG REQUIRED)
find_dependency(date CONFIG REQUIRED)

if (@HAVE_BOOST_FILESYSTEM@)
    find_dependency(Boost COMPONENTS filesystem)
endif ()

if (@INFRA_GDAL@)
    find_dependency(GDAL CONFIG)
    if (NOT TARGET GDAL::GDAL)
        find_dependency(Gdal REQUIRED)
    endif ()
    find_dependency(Gsl REQUIRED)
endif ()

if (@INFRA_LOGGING@)
    find_dependency(spdlog CONFIG REQUIRED)
endif ()

if (@INFRA_XML@)
    find_dependency(pugixml REQUIRED)
endif ()

if (@INFRA_CHARSET@)
    find_dependency(ICU COMPONENTS i18n data uc REQUIRED)
endif ()

if (@INFRA_HASHING@)
    find_dependency(cryptopp CONFIG REQUIRED)
endif ()

if (@INFRA_PROCESS@)
    find_dependency(reproc++ CONFIG REQUIRED)
endif ()

if (@INFRA_CLI_PROGRESS@)
    find_dependency(indicators CONFIG REQUIRED)
endif ()

if (@INFRA_NUMERIC@)
    find_dependency(Eigen3 CONFIG REQUIRED)
endif ()

if (@INFRA_DATABASE@)
    find_dependency(Sqlpp11 CONFIG REQUIRED)
    if (@INFRA_DATABASE_SQLITE@)
        find_dependency(Sqlpp11 CONFIG REQUIRED COMPONENTS SQLite3)
    endif ()
    if (@INFRA_DATABASE_POSTGRES@)
        find_dependency(Sqlpp11 CONFIG REQUIRED COMPONENTS PostgreSQL)
    endif ()
endif ()

if (@INFRA_COMPRESSION_ZSTD@)
    find_dependency(zstd CONFIG REQUIRED)
endif ()

if ("testutil" IN_LIST Infra_FIND_COMPONENTS)
    if (@INFRA_ENABLE_TEST_UTILS@)
        find_dependency(doctest CONFIG REQUIRED)
    elseif (Infra_FIND_REQUIRED_testutils)
        message(FATAL_ERROR "Infra testutil component requested but Infra was compiled without test support")
    endif ()
endif()

if ("ui" IN_LIST Infra_FIND_COMPONENTS)
    if (@INFRA_UI_COMPONENTS@)
        find_dependency(Qt5Gui REQUIRED)
        find_dependency(Qt5Widgets REQUIRED)
        find_dependency(Qt5Core REQUIRED)
        find_dependency(Qt5Network REQUIRED)

        if (@INFRA_UI_COMPONENTS_QML@)
            find_dependency(Qt5Quick CONFIG REQUIRED)
        endif ()

        if (@INFRA_UI_COMPONENTS_LOCATION@)
            find_dependency(Qt5Location CONFIG REQUIRED)
        endif ()

        if (@INFRA_UI_COMPONENTS_XLSX_EXPORT@)
            find_dependency(XlsxWriter REQUIRED)
        endif ()
    elseif (Infra_FIND_REQUIRED_ui)
        message(FATAL_ERROR "Infra ui component requested but Infra was compiled without ui support")
    endif ()
endif ()

list(REMOVE_AT CMAKE_MODULE_PATH -1)

if(NOT TARGET Infra::infra)
    include("${Infra_CMAKE_DIR}/InfraTargets.cmake")
endif()

if(NOT TARGET Infra::infratestutil AND "testutil" IN_LIST Infra_FIND_COMPONENTS)
    include("${Infra_CMAKE_DIR}/InfraTestTargets.cmake")
endif()

if(NOT TARGET Infra::uiinfra AND "ui" IN_LIST Infra_FIND_COMPONENTS)
    include("${Infra_CMAKE_DIR}/InfraUiTargets.cmake")
endif()

set(Infra_LIBRARIES Infra::infra)
