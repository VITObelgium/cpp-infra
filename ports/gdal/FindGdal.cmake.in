# Defines the Gdal::Gdal target
# Defines the GDAL_DATA_PATH variable containing the path of the gdal data files
# needed for coordinate calculations

include(FindPackageHandleStandardArgs)

find_path(Gdal_INCLUDE_DIR
    NAMES gdal.h
    HINTS ${Gdal_ROOT_DIR}/include ${Gdal_INCLUDEDIR}
)

find_library(Gdal_LIBRARY NAMES gdal HINTS ${Gdal_ROOT_DIR}/lib)
find_library(Gdal_LIBRARY_DEBUG NAMES gdald HINTS ${Gdal_ROOT_DIR}/lib)

foreach(PPATH IN LISTS CMAKE_PREFIX_PATH)
    if (EXISTS ${PPATH}/share/gdal)
        file(TO_CMAKE_PATH ${PPATH}/share/gdal Gdal_DATA_PATH)
    endif ()
endforeach()

message(STATUS "Gdal library: ${Gdal_LIBRARY}")
message(STATUS "Gdal data path: ${Gdal_DATA_PATH}")

find_package(TIFF REQUIRED QUIET)
find_package(ZLIB REQUIRED QUIET)
find_package(PROJ4 CONFIG REQUIRED QUIET)
find_package(unofficial-iconv CONFIG REQUIRED QUIET)

if (NOT EMSCRIPTEN)
    find_package(Threads)
endif ()

find_package_handle_standard_args(Gdal
    FOUND_VAR Gdal_FOUND
    REQUIRED_VARS Gdal_INCLUDE_DIR Gdal_LIBRARY ZLIB_FOUND
)

mark_as_advanced(
    Gdal_ROOT_DIR
    Gdal_INCLUDE_DIR
    Gdal_LIBRARY
    Gdal_LIBRARY_DEBUG
)

if(Gdal_FOUND AND NOT TARGET Gdal::Gdal)
    add_library(Gdal::Gdal STATIC IMPORTED)
    set_target_properties(Gdal::Gdal PROPERTIES
        IMPORTED_LINK_INTERFACE_LANGUAGES "CXX"
        INTERFACE_INCLUDE_DIRECTORIES "${Gdal_INCLUDE_DIR}"
        IMPORTED_LOCATION ${Gdal_LIBRARY}
    )

    if(Gdal_LIBRARY_DEBUG)
        set_target_properties(Gdal::Gdal PROPERTIES
            IMPORTED_LOCATION_DEBUG "${Gdal_LIBRARY_DEBUG}"
        )
    endif()

    # required dependencies
    set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES TIFF::TIFF proj ZLIB::ZLIB unofficial::iconv::libcharset unofficial::iconv::libiconv)

    # optional dependencies
    if (@WITH_EXPAT@)
        find_package(EXPAT REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES EXPAT::EXPAT)
    endif ()

    # on windows the gif library is built into the gdal library
    if (@WITH_GIF@ AND UNIX) 
        find_package(GIF REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${GIF_LIBRARIES})
    endif ()

    if (@WITH_GEOS@)
        find_package(Geos REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES Geos::Geosc)
    endif ()

    if (@WITH_PNG@)
        find_package(PNG REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES PNG::PNG)
    endif ()

    if (@WITH_JPEG@)
        find_package(JPEG REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES JPEG::JPEG)
    endif ()

    if (@WITH_NETCDF@)
        find_package(hdf5 CONFIG REQUIRED QUIET)
        find_package(netCDF CONFIG REQUIRED QUIET)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES netcdf hdf5::hdf5-static hdf5::hdf5_hl-static)
    endif ()

    if (UNIX AND NOT EMSCRIPTEN)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES Threads::Threads ${CMAKE_DL_LIBS})
    endif ()

    if (WIN32)
        set_property(TARGET Gdal::Gdal APPEND PROPERTY INTERFACE_LINK_LIBRARIES ws2_32 psapi)
    endif ()
endif()